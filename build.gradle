buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url "https://jitpack.io"
        }
    }
    dependencies {
        classpath 'com.github.GTNewHorizons:ForgeGradle:1.2.4'
    }
}

//add dependency repositories
repositories {
	maven {
		name = "chickenbones"
		url = "http://chickenbones.net/maven"
	}
    maven {
        url "https://jitpack.io"
    }
}

apply plugin: 'forge'

group = "codechicken" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "EnderStorage"

// Define properties file
ext.configFile = file "build.properties"

configFile.withReader {
    // Load config.  It shall from now be referenced as simply config or project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

dependencies {
    compile "com.github.GTNewHorizons:NotEnoughItems:master-SNAPSHOT:dev"
}

version =  "${project.config.mod_version}." + System.getenv("BUILD_NUMBER") ?: "1"

println  config.mc_version + "-" + config.forge_version
// Setup the forge minecraft plugin data. Specify the preferred forge/minecraft version here
minecraft {
    version = config.mc_version + "-" + config.forge_version
}

//def root = project.projectDir.parentFile
sourceSets {
    main {
        java {
            srcDir new File("src")
        }
        resources {
            srcDir new File("resources")
        }
    }
}

processResources {
    //redo task if any of these properties change
    inputs.property "version", project.version
    inputs.property "mc_version", config.mc_version

    // Replace properties in info files
    from(sourceSets.main.resources.srcDirs) {
        include '*.info'
        expand 'version':project.version,'mc_version':config.mc_version
    }
    // Copy everything else
    from(sourceSets.main.resources.srcDirs) {
        include 'assets/**/*.*'
    }
}

version = "${project.minecraft.version}-${project.version}"

jar {
    classifier = 'universal'
}

task sourceJar(type: Jar) {
    from sourceSets.main.java
    from new File("dev")
    classifier = 'src'
}

task devJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

// Tell the artifact system about our extra jars
artifacts {
    archives sourceJar, devJar
}

// Configure an upload task.
uploadArchives {
    dependsOn 'reobf'
	if (project.hasProperty("local_maven")) {
		repositories {
			logger.info('Publishing to local maven repo')

			mavenDeployer {
				repository(url: "file://${local_maven}")
			}
		}
	}
}